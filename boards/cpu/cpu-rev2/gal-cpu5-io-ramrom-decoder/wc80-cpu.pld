Name        wc80-cpu             ;
Partno                           ;
Revision    1                    ;
Date        06/23/2021           ;
Designer                         ;
Company     alnwlsn              ;
Assembly    None                 ;
Location                         ;
Device      g22v10               ;
Format      jedec                ;

/** cpu5 board io decoder and glue logic **/

/** decodes IO addresses for Mem bank selector and LED Display **/
/** also operates the Direction signal for the bus buffer **/

/** Inputs  **/
Pin 1   =  nOUT; /** io write from or gate (TRS80 *OUT) **/
Pin 2   =  nMEMRQ; /** z80 memrq pin (or TRS80 *RAS) **/
Pin 3   =  A7;
Pin 4   =  A6;
Pin 5   =  A5;
Pin 6   =  A4;
Pin 7   =  A3;
Pin 8   =  A2;
Pin 9   =  A1; 
Pin 10  =  A0; 
Pin 11  =  nRD;
Pin 13  =  nINUSE; /** from external mem space use: when low, disables RAM and ROM from the bus **/
Pin 14  =  MEMCHIP; /** select beteween RAM chip and ROM chip **/
Pin 15  =  UNUSED; /** was meant to select between 2 sets of roms/rams; currently unused  **/

/** Outputs **/
Pin 16  =  nBANKSEL; /** to /WE strobe on bank select chips **/
Pin 17  =  nROM0;    /** to chip select on RAM **/
Pin 18  =  nRAM0;   /** to chip select on ROM **/
Pin 19  =  CASSOUT;  
Pin 20  =  CASSIN;
Pin 21  =  CLKDIV;    /** to /WE strobe on LED dsiplay **/
Pin 22  =  nBUSBUF;   /** to DIR pin on Data transeiver **/
Pin 23  =  nIORQ; 

/** Logic Equations **/
!nBANKSEL = (!nOUT & !A7 & A6 & A5 & A4 & !A3 & !A2); /** IO write to 0x70 - 0x73 (register file selectors)**/

!CASSOUT  = (!nOUT & A7 & A6 & A5 & A4 & A3 &  A2 & A1 & A0);
!CASSIN   = (!nIORQ & !nRD & A7 & A6 & A5 & A4 & A3 &  A2 & A1 & A0); 

!CLKDIV   = (!nOUT & !A7 & A6 & A5 & A4 & !A3 &  A2 & !A1 & !A0);

!nRAM0 = nINUSE & !nMEMRQ & MEMCHIP;
!nROM0 = nINUSE & !nMEMRQ & !MEMCHIP;

nBUSBUF = (nRD # nIORQ # !CASSIN) & (nINUSE # nMEMRQ # nRD); /** read from the bus when: io reads happen, and when off board ram is being read, and when cassette in is not being used **/
